# Script: sim_sequences.Rev
# Authors: Sarah K. Swiston (sarah.k.swiston@wustl.edu)
# Date: January 15, 2024

# This script will:
# (1) Read in a simulation generated by PhyloJunction
# (2) Graft on an outgroup with the appropriate tip state
# (3) Simulate sequences for the whole tree
#
# NOTE: Run this script from hawaiian_simulations/

#################
# Initial Setup #
#################

if (!exists("i")) i = 2 #9999
if (!exists("exp_path")) exp_path = "experiment1/"
if (!exists("continent_region_idx")) continent_region_idx = 7

out_path = exp_path + "sim_data/"
tree_fp = out_path + "sample" + i + ".tre"

###################
# Adding Outgroup #
###################

if (fileExists(tree_fp)) {
    # Remove all extinct taxa from the tree
    phy = readTrees(file=tree_fp)[1]

########################
# Simulating Sequences #
########################

    # Simulate sequences over the full tree
    # molecular model
    mu_mol_base ~ dnLoguniform(5e-4, 5e-2)
    mol_fp = out_path + "sample" + i + ".mol.tsv"
    write(mu_mol_base,filename=mol_fp,sep="\t")

    # Jukes-Cantor matrix
    Q <- fnJC(4)
    
    # branch rate variation
    mu_mol_sd <- 0.587405 
    
    # relative among branch rate variation
    num_branches = 2 * phy.ntips() - 2
    if (exists("mu_mol_branch")) {clear(mu_mol_branch)}
    for (j in 1:num_branches) {
        ln_mean := ln(mu_mol_base) - 0.5 * mu_mol_sd * mu_mol_sd
        mu_mol_branch[j] ~ dnLnorm(ln_mean, mu_mol_sd)
    }
    branch_fp = out_path + "sample" + i + ".branches.tsv"
    write(mu_mol_branch,filename=branch_fp,sep="\t")    

    # Sequence model
    seq ~ dnPhyloCTMC(tree=phy,Q=Q,branchRates=mu_mol_branch,nSites=2000,type="DNA")

    # Save the sequence data in sim_data
    seq_fp = out_path + "sample" + i + ".sequences.nex"
    writeNexus(seq_fp,seq)
    print("Simulated sequences")
}

q()

